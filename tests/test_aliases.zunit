#!/usr/bin/env zunit

@setup {
    load "../you-should-use.plugin.zsh"
}

@teardown {
}

@test 'aliases no match' {
    alias ls="less -R"
    run _check_aliases "less"

    assert "$output" is_empty
    assert $state equals 0
}

@test 'aliases longer or the same length as their commands are ignored' {
    alias longalias='ls -lA'
    alias ls_-lA='ls -lA'
    run _check_aliases "ls -lA"

    assert "$output" is_empty
    assert $state equals 0
}

@test 'aliases match - all with alphabetical ordering' {
    export YSU_MODE="ALL"
    alias c="git status -v"
    alias a="git status"
    alias b="git"
    run _check_aliases "git status -v"

    assert 'Found existing alias for "git status". You should use: "a"' is_substring_of "$lines[1]"
    assert 'Found existing alias for "git". You should use: "b"' is_substring_of "$lines[2]"
    assert 'Found existing alias for "git status -v". You should use: "c"' is_substring_of "$lines[3]"
    assert $state equals 0
}

@test 'aliases bestmatch default' {
    alias gs="git status"
    alias g="git"
    run _check_aliases "git status -v"

    assert 'Found existing alias for "git status". You should use: "gs"' is_substring_of "$output"
    assert $state equals 0
}

@test 'aliases bestmatch - chooses longest matching alias value' {
    export YSU_MODE="BESTMATCH"
    alias gs="git status"
    alias g="git"
    alias gsv="git status -v"
    run _check_aliases "git status -v"

    assert 'Found existing alias for "git status -v". You should use: "gsv"' is_substring_of "$output"
    assert $state equals 0
}

@test 'aliases bestmatch - chooses shortest alias key on equal value lengths' {
    export YSU_MODE="BESTMATCH"
    alias v="vim"
    alias nvim="vim"
    run _check_aliases "vim"

    assert 'Found existing alias for "vim". You should use: "v"' is_substring_of "$output"
    assert $state equals 0
}

@test 'aliases exact match' {
    alias hu='hg update'
    run _check_aliases 'hg update'

    assert 'Found existing alias for "hg update". You should use: "hu"' is_substring_of "$output"
    assert $state equals 0
}

@test 'aliases substring match' {
    alias hu='hg update'
    run _check_aliases 'hg update -all'

    assert 'Found existing alias for "hg update". You should use: "hu"' is_substring_of "$output"
    assert $state equals 0
}
